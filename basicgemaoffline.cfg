// differences to server: item respawn rate, no flag reset, /gonext not usable
// additional ideas : timetarget, countdown, rounding if milliseconds are longer than 3 characters
// can be used for : offline training, compare your personal records on different servers (independent of your ping)

check2init gematoggle 0
check2init showname 0
check2init showweapon 1

checkinit mapstartalways [ if (= $gematoggle 1)[(sendMOTD)]]

alias sendMOTD [
if (!= (getbestrecord) "3no record")[echo (c J) "Your best time for this map is " (getbestrecord)][echo "3You don't have records on this map"];
]

checkinit onSpawn [ if (= $gematoggle 1) [if (= $arg1 (player1 cn)) [ start_time = (millis)]]]

checkinit onFlag [ 
if (= $gematoggle 1) [
	if (&& (= $arg1 4) (= $arg2 (player1 cn)) (= $gamemode (modenum ctf)) ) [
		if (> $start_time 0) [
			tmp_record = (- (millis) $start_time);
			echo (getscoretext $tmp_record);
			tmp_name = (concatword (curmap) (currentprimary));
			start_time = 0;
			if (= (isIdent $tmp_name) 0)[ 
			(concatword (curmap) (currentprimary)) =  $tmp_record;
			echo (c 9) "*** new best time ***";			// referring to the current primary weapon, not to all time best
			][if (< $tmp_record ($tmp_name)) [($tmp_name) =  $tmp_record;
				echo (c 9) "*** new best time ***";			// referring to the current primary weapon, not to all time best
			][echo (c 9) "(but has a better record)"]]
]]]]

alias getscoretext [
	tmp_text = (concatword (c H) "You");
	if (= $showname 0) [tmp_text = (concatword (c H) (player (player1 cn) name))]
	tmp_text = (concatword $tmp_text (c J) " scored after " (c F) (millitohuman $tmp_record));
	if (= $showweapon 1) [tmp_text = (concatword $tmp_text " with " (c 1) (weaponname (currentprimary)))];
	return $tmp_text;
]

alias millitohuman [
	seconds = (div $arg1 1000);
	minutes = (div $seconds 60);
	seconds = (- $seconds (* $minutes 60))
	milliseconds = (- $arg1 (* $seconds 1000) (* $minutes 60000));
	
	if (< $milliseconds 100) [milliseconds = (concatword "0" $milliseconds)]
	if (< $milliseconds 10) [milliseconds = (concatword "0" $milliseconds)]
	if (< $seconds 10) [seconds = (concatword "0" $seconds)];
	if (< $minutes 10) [minutes = (concatword "0" $minutes)];
	
	return (concatword $minutes ":" $seconds ":" $milliseconds)
]

alias weaponname [
	if (= $arg1 6) [return "Assault Rifle"][ 
	if (= $arg1 4) [return "Submachine Gun"][ 
	if (= $arg1 5) [return "Sniper Rifle"][
	if (= $arg1 3) [return "Shotgun"][ 
	if (= $arg1 2) [return "Carbine"]]]]]
]

alias getbestrecord [
	best_record = 0;
	loop i 5 [
		tmp_weapon = (- 6 $i);
		tmp_var = (concatword (curmap) $tmp_weapon);
		if (= (isIdent $tmp_var) 1) [
			
			tmp_record = ($tmp_var);
		
			if (!= $tmp_record $tmp_var)[if (|| (= $best_record 0) (> $best_record $tmp_record) ) [best_record = $tmp_record;break]]
		]
	]
	if (= $best_record 0) [return "3no record"][return (concatword "8" (millitohuman $best_record) " J(1" (weaponname $tmp_weapon) "J)")]
]

alias getrecord [
	tmp_name = (concatword (curmap) $arg1);
	if (= (isIdent $tmp_name) 1) [
		return (concat "8" (millitohuman ($tmp_name)));
	][return "3no record";]
]

//user interface

newmenu "gema offline"
menuitemcheckbox "9Enable gema mode" "$gematoggle" [gematoggle = $arg1]
menuitemvar [concat "Jmap : P " (curmap)][][]
menuitemvar [concat "9best time: " (getbestrecord)] [showmenu "records"] []
menuitemcheckbox "JShow HYou" "$showname" [ showname = $arg1 ]
menuitemcheckbox "JShow 1weapon" "$showweapon" [showweapon = $arg1]
menuitem "Close"	[ closemenu ]

newmenu "records"
menuitemvar [concat "Jmap : P " (curmap)] [][]
menuitemvar [concat "1Assault Rifle: " (getrecord 6)] [(delalias (concatword (curmap) "6"))] []
menuitemvar [concat "1Submachine Gun: " (getrecord 4)] [(delalias (concatword (curmap) "4"))] []
menuitemvar [concat "1Sniper Rifle: " (getrecord 5)] [(delalias (concatword (curmap) "5"))] []
menuitemvar [concat "1Shotgun: " (getrecord 3)] [(delalias (concatword (curmap) "3"))] []
menuitemvar [concat "1Carbine: " (getrecord 2)] [(delalias (concatword (curmap) "2"))] []

bind l [showmenu "gema offline"]
