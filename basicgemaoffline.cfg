// differences to server: item respawn rate, no flag reset, /gonext not usable
// can be used for : offline training, store your personal records

check2init gematoggle 0
check2init showweapon 1
check2init specialmode 0
check2init scoreweapon 6
check2init start_time 0
check2init timetarget_milli 000
check2init timetarget_sec 00
check2init timetarget_min 0
check2init timetargetmode 0
check2init countdowntoggle 0
check2init timetarget 0

checkinit mapstartalways [if (&& $gematoggle (! (isgema))) [gematoggle = 0; echo (c 3) "gema mode was deactivated (map is not a gema or mode is not ctf)"]; if $gematoggle sendMOTD; abortrun]

alias isgema [
	if (&& (strcmp (getmode) "ctf") (gemamap (curmap))) [return 1][return 0]
]

alias gemamap [
	mapnames = ["g3ma" "g3m@" "g3m4" "gema" "gem@" "gem4"]
	isgemamap = 0;

	loop i (listlen $mapnames) [
		if (strstr (tolower $arg1) (at $mapnames $i)) [isgemamap = 1; break][isgemamap = 0]
	]
	
	return $isgemamap;
]

checkinit onAttack [
	if (&& $specialmode (&& $gematoggle (> $start_time 0))) [
		if (&& (!= $arg1 0) (!= $arg1 8)) [
			if (= $scoreweapon 0) [
				if (= $arg1 9) [ scoreweapon = 1][scoreweapon = $arg1];
				echo (concat (c 3) "knife only aborted!" (c 9) "But you can still finish with" (c 1) (weaponname $scoreweapon))
			][
				if (= $scoreweapon 1) [
					if (&& (!= $arg1 1) (!= $arg1 9))[
						scoreweapon = (currentprimary);
						echo (concat (c 3) "pistol only aborted!" (c 9) "But you can still finish with" (c 1) (weaponname $scoreweapon))
					]
				]
			]
		]
	]
]

checkinit onSpawn [
	if (&& $gematoggle (= $arg1 (player1 cn))) [
		start_time = (millis); 
		checkedit;
		checkteam;
		scoreweapon = (currentprimary);
		if $specialmode [scoreweapon = 0];
		if (&& $timetargetmode $countdowntoggle) [countdown $timetarget]
	]
]

alias abortrun [ start_time = 0; loop i 10 [resetsleeps]]

alias checkedit [ if (&& $gematoggle $editing) [abortrun; echo (c 3) "run aborted because you switched to edit mode!"][sleep 0 checkedit]]
alias checkteam [ if (&& $gematoggle (! (alive))) [abortrun][sleep 0 checkteam]]

alias sendMOTD [
	if (!= (getbestrecord) "3no record")[
		echo (concatword (c J) "Your best time for this map is " (getbestrecord));
		echo (concat (c 3) "missing weapons :" (missingweapons))
	][
		echo (concatword (c 3) "You don't have records on this map")
	]
]

alias missingweapons [
	tmp_text = "";
	loop i 7 [
		if (! (isIdent (concatword (curmap) $i)))[
			if (= (strlen $tmp_text) 0)[tmp_text = (weaponname $i)][tmp_text = (concatword $tmp_text ", " (weaponname $i))]
		]
	]
	
	if (= (strlen $tmp_text) 0) [tmp_text = "none"]
	return $tmp_text;
]

checkinit onFlag [
	if (&& $gematoggle (&& (= $arg1 4) (= $arg2 (player1 cn)))) [
		if (> $start_time 0) [
			tmp_end = (millis);
			tmp_record = (- $tmp_end $start_time);
			
			user_record = (millitohuman $tmp_record);
			tmp_text = (concatword (c J) "You scored after " (c F) $user_record);
			if (= $showweapon 1) [tmp_text = (concatword $tmp_text " with " (c 1) (weaponname $scoreweapon))];
			
			echo $tmp_text;
			
			tmp_name = (concatword (curmap) $scoreweapon);
			
			tmp_text = "";
			
			if (! (isIdent $tmp_name))[ 
				$tmp_name =  $tmp_record;
				tmp_text = (concat (c 9) "*** new best time for this weapon ***");
			][
				if (< $tmp_record ($tmp_name)) [
					$tmp_name =  $tmp_record;
					tmp_text = (concat (c 9) "*** new best time for this weapon ***")
				][
					tmp_text = (concat (c 9) "(but you have a better record)")
				]
			]
			
			if (strstr (getbestrecord) $user_record) [
				tmp_text = (concat (c 9) "*** new all time best time ***")
			]
			
			echo $tmp_text;
			
			if $timetargetmode [
				if (< $tmp_record $timetarget)[
					echo (c 2) "*** " (c Q) "you were faster than your time target" (c 2) " ***"
				][
					if (= $tmp_record $timetarget)[
						echo (c 9) "You were exactly as fast as your time target"
					][
						if (> $tmp_record $timetarget)[
							echo (c 8) "You were slower than your time target"
						]
					]
				]
			]
			
			abortrun
		]
	]
]

alias millitohuman [
	seconds = (div $arg1 1000);
	minutes = (div $seconds 60);
	seconds = (- $seconds (* $minutes 60))
	milliseconds = (- $arg1 (* $seconds 1000) (* $minutes 60000));
	
	if (< $milliseconds 100) [milliseconds = (concatword "0" $milliseconds)][
		if (< $milliseconds 10) [milliseconds = (concatword "00" $milliseconds)]
	]
	if (< $seconds 10) [seconds = (concatword "0" $seconds)];
	if (< $minutes 10) [minutes = (concatword "0" $minutes)];
	
	return (concatword $minutes ":" $seconds "," $milliseconds)
]

alias weaponname [
	if (= $arg1 0) [return "Knife"][
	if (= $arg1 1) [return "Pistol"][
	if (= $arg1 2) [return "Carbine"][
	if (= $arg1 3) [return "Shotgun"][
	if (= $arg1 4) [return "Submachine Gun"][ 
	if (= $arg1 5) [return "Sniper Rifle"][
	if (= $arg1 6) [return "Assault Rifle"][
	if (= $arg1 8) [return "Grenade"][
	if (= $arg1 9) [return "Akimbo"]
	]]]]]]]]
]

alias getbestrecord [
	best_record = 0;
	best_weapon = 0;
	
	loop i 7 [
		tmp_weapon = (- 6 $i);
		tmp_var = (concatword (curmap) $tmp_weapon);
		if (isIdent $tmp_var) [
			record = ($tmp_var);
			if (!= $record $tmp_var)[
				if (|| (= $best_record 0) (> $best_record $record) ) [best_record = $record; best_weapon = $tmp_weapon]
			]
		]
	]
	
	if (= $best_record 0) [return "3no record"][return (concatword "8" (millitohuman $best_record) " J(1" (weaponname $best_weapon) "J)")]
]

alias getrecord [
	tmp_name = (concatword (curmap) $arg1);
	if (isIdent $tmp_name) [ return (millitohuman ($tmp_name))][return "3no record";]
]

alias countdown [
	counter = 0;
	seconds = (div $arg1 1000);
	milliseconds = (- $arg1 (* 1000 $seconds));

	sleep $milliseconds [
		sleep (* $seconds 1000) [echo (c 3) "time target finished"]
		loop i $seconds [
			sleep (* $i 1000)[
				remaining = (- $seconds $counter);
				
				color = "J";
				
				if (< $remaining 10)[if (< $remaining 4)[color = "3"][color = "9"]]
				
				echo (c $color) (formatcountdown $remaining);
				++ counter;
			]
		]
	]
]

alias formatcountdown [
	min = (div $arg1 60);
	sec = (- $arg1 (* $min 60));
	
	if (< $sec 10) [sec = (concatword "0" $sec)];
	if (< $min 10) [min = (concatword "0" $min)];
	
	return (concatword $min ":" $sec);
]

//user interface

newmenu "gema offline"
menuitemcheckbox "9Enable gema mode" "$gematoggle" [ if (! (isgema)) [echo (c 3) "can not acitvate gema mode! Map is not a gema or mode is not ctf."][gematoggle = $arg1]]
menuitemvar [concat "Jmap : P " (curmap)][][]
menuitemvar [concat "9best time: " (getbestrecord)] [showmenu "records"] []
menuitemvar [concatword "2time target: " $timetarget_min ":" $timetarget_sec "," $timetarget_milli] [showmenu "time target"]
menuitemcheckbox "JEnable Knife/Pistol only" "$specialmode" [ specialmode = $arg1 ]
menuitemcheckbox "JShow 1weapon" "$showweapon" [showweapon = $arg1]
menuitem "browse gema servers" [showmenu "gema servers"]
menuitem "Close"	[ closemenu ]

newmenu "time target"
menuitemvar [concat "Jmap : P " (curmap)] [][]
menuitemtextinput "8Minutes" "$timetarget_min" [timetarget_min = $arg1; calctarget]
menuitemtextinput "8Seconds" "$timetarget_sec" [timetarget_sec = $arg1; if (< $timetarget_sec 10)[timetarget_sec = (concatword "0" $timetarget_sec); calctarget]]
menuitemtextinput "8Milliseconds" "$timetarget_milli" [timetarget_milli = $arg1; calctarget; if (< $timetarget_milli 10)[timetarget_milli = (concatword "00" $timetarget_milli)][if (< $timetarget_milli 100)[timetarget_milli = (concatword "0" $timetarget_milli)]]]
menuitemcheckbox "2Activate Time Target Mode" "$timetargetmode" [ timetargetmode = $arg1]
menuitemcheckbox "2Show countdown" "$countdowntoggle" [ countdowntoggle = $arg1 ]

alias calctarget [
timetarget = (+ (* $timetarget_min 60000) (+ (* $timetarget_sec 1000) $timetarget_milli));
]

newmenu "records"
menuitemvar [concat "Jmap : P " (curmap)] [][]
menuitemvar [concat "1Assault Rifle: 8" (getrecord 6)] [(delalias (concatword (curmap) "6"))] []
menuitemvar [concat "1Submachine Gun: 8" (getrecord 4)] [(delalias (concatword (curmap) "4"))] []
menuitemvar [concat "1Sniper Rifle: 8" (getrecord 5)] [(delalias (concatword (curmap) "5"))] []
menuitemvar [concat "1Shotgun: 8" (getrecord 3)] [(delalias (concatword (curmap) "3"))] []
menuitemvar [concat "1Carbine: 8" (getrecord 2)] [(delalias (concatword (curmap) "2"))] []
menuitemvar [concat "1Pistol only: 8" (getrecord 1)] [(delalias (concatword (curmap) "1"))] []
menuitemvar [concat "1Knife only: 8" (getrecord 0)] [(delalias (concatword (curmap) "0"))] []

bind l [showmenu "gema offline"]

newmenu "gema servers"
menuitem "GEMA Avengers" [connect 84.200.52.36 28763]
menuitem "Gema Central" [connect 185.38.45.43 1111]
menuitem "Silvercloud GEMAS" [connect 162.220.8.73 28700]
