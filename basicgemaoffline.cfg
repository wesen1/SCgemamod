// differences to server: item respawn rate, no flag reset, /gonext not usable
// can be used for : offline training, compare your personal records on different servers

// additional ideas : timetarget, countdown, rounding if milliseconds are longer than 3 characters

// bug : onweaponswitch doesnt recognize switch to akimbo on pickup

// not finished yet


// hudecho für rekord; knife only in verbindung mit allowblinkingtext
// format für scoretext
// getmode		(= (getmode) "ctf")
// check mapname for gema mode?
// evtl curteam einbauen
// menuitemtextinput
// onAttack -> kein primary oder pistol vor knife only mode verwenden

check2init gematoggle 0
check2init showweapon 1
check2init specialmode 0
check2init scoreweapon 6
check2init start_time 0

checkinit mapstartalways [ if $gematoggle [(sendMOTD)]]

checkinit onSpawn [
	if (&& $gematoggle (= $arg1 (player1 cn))) [
		start_time = (millis); 
		checkedit;
		checkteam;
		scoreweapon = (currentprimary);
		if $specialmode [specialcountdown]
	]
]

checkinit onWeaponSwitch [
	if (&& $specialmode (&& $gematoggle (> $start_time 0)))[
		if (&& (!= $arg1 0) (!= $arg1 8)) [
			if (= $scoreweapon 0) [
				if (= $arg1 1) [scoreweapon = 1][scoreweapon = (currentprimary)];
				echo (concat (c 3) "knife only aborted!" (c 9) "But you can still finish with" (c 1) (weaponname $scoreweapon))
			][
				if (&& (= $scoreweapon 1) (!= $arg1 1))[
					scoreweapon = (currentprimary);
					echo (concat (c 3) "pistol only aborted!" (c 9) "But you can still finish with" (c 1) (weaponname $scoreweapon))
				]
			]
		]
	]
]

alias specialcountdown [
	counter = 0;
	echo (c 3) "switch to knife or pistol now!";
	loop i 6 [
		sleep (* $i 1000) [
			++ counter;
			
			if (&& (!= $scoreweapon 0) (!= $scoreweapon 1))[
				
				if (|| (= (curweapon) 0) (= (curweapon) 8)) [
					echo (c J) "knife only mode started";
					scoreweapon = 0;
				][
					if (= (curweapon) 1)[
					echo (c J) "pistol only mode started";
					scoreweapon = 1;
					][
						echo (c 9) (concatword (- 6 $counter) "...");
						if (= $counter 6)[
							scoreweapon = (currentprimary);
							echo (concat (c 3) "Special mode aborted!" (c 9) "But you can still finish with" (c 1) (weaponname $scoreweapon))
						]
					]
				
				]
				
			]
		]
	]
]

alias abortrun [ start_time = 0; loop i 10 [resetsleeps]]

alias checkedit [ if (&& $gematoggle $editing) [abortrun; echo (c 3) "run aborted because you switched to edit mode!"][sleep 0 checkedit]]
alias checkteam [ if (&& $gematoggle (! (alive))) [abortrun][sleep 0 checkteam]]

alias sendMOTD [
if (!= (getbestrecord) "3no record")[echo (c J) "Your best time for this map is " (getbestrecord)][echo (c 3) "You don't have records on this map"];
]

checkinit onFlag [
if $gematoggle [
	if (&& (= $arg1 4) (= $arg2 (player1 cn))) [
		if (> $start_time 0) [
			tmp_record = (- (millis) $start_time);
			abortrun;
			echo (getscoretext $tmp_record);
			tmp_name = (concatword (curmap) $scoreweapon);
			
			if (! (isIdent $tmp_name))[ 
			(concatword (curmap) $scoreweapon) =  $tmp_record;
			echo (c 9) "*** new best time ***";
			][if (< $tmp_record ($tmp_name)) [($tmp_name) =  $tmp_record;
				echo (c 9) "*** new best time ***";
			][echo (c 9) "(but you have a better record)"]]
]]]]

alias getscoretext [
	tmp_text = (concatword (c J) "You scored after " (c F) (millitohuman $tmp_record));
	if (= $showweapon 1) [tmp_text = (concatword $tmp_text " with " (c 1) (weaponname $scoreweapon))];
	return $tmp_text;
]

alias millitohuman [
	seconds = (div $arg1 1000);
	minutes = (div $seconds 60);
	seconds = (- $seconds (* $minutes 60))
	milliseconds = (- $arg1 (* $seconds 1000) (* $minutes 60000));
	
	if (< $milliseconds 100) [milliseconds = (concatword "0" $milliseconds)]
	if (< $milliseconds 10) [milliseconds = (concatword "0" $milliseconds)]
	if (< $seconds 10) [seconds = (concatword "0" $seconds)];
	if (< $minutes 10) [minutes = (concatword "0" $minutes)];
	
	return (concatword $minutes ":" $seconds ":" $milliseconds)
]

alias weaponname [
	if (= $arg1 0) [return "Knife"][
	if (= $arg1 1) [return "Pistol"][
	if (= $arg1 2) [return "Carbine"][
	if (= $arg1 3) [return "Shotgun"][
	if (= $arg1 4) [return "Submachine Gun"][ 
	if (= $arg1 5) [return "Sniper Rifle"][
	if (= $arg1 6) [return "Assault Rifle"][
	if (= $arg1 8) [return "Grenade"][
	if (= $arg1 9) [return "Akimbo"]
	]]]]]]]]
]

alias getbestrecord [
	best_record = 0;
	best_weapon = 0;
	
	loop i 7 [
		tmp_weapon = (- 6 $i);
		tmp_var = (concatword (curmap) $tmp_weapon);
		if (isIdent $tmp_var) [
			tmp_record = ($tmp_var);
			if (!= $tmp_record $tmp_var)[
				if (|| (= $best_record 0) (> $best_record $tmp_record) ) [best_record = $tmp_record; best_weapon = $tmp_weapon]
			]
		]
	]
	
	if (= $best_record 0) [return "3no record"][return (concatword "8" (millitohuman $best_record) " J(1" (weaponname $best_weapon) "J)")]
]

alias getrecord [
	tmp_name = (concatword (curmap) $arg1);
	if (isIdent $tmp_name) [
		return (concat "8" (millitohuman ($tmp_name)));
	][return "3no record";]
]

alias countdown [
	counter = 0;
	seconds = (div $arg1 1000);
	milliseconds = (- $arg1 (* 1000 $seconds));

	sleep $milliseconds [
		loop i $seconds [
			sleep (* $i 1000)[
				remaining = (- $seconds $counter);
			
				if (< $remaining 10)[
					if (< $remaining 4)[echo (c 3) (millitohuman (* $remaining 1000))][echo (c 9) (millitohuman (* $remaining 1000))]
				][echo (c J) (millitohuman (* $remaining 1000))]
				
				++ counter;
			]
		]
	]
]

//user interface

newmenu "gema offline"
menuitemcheckbox "9Enable gema mode" "$gematoggle" [gematoggle = $arg1; if (! $gematoggle) [editcheck = 0]]
menuitemvar [concat "Jmap : P " (curmap)][][]
menuitemvar [concat "9best time: " (getbestrecord)] [showmenu "records"] []
menuitemcheckbox "JEnable Knife/Pistol only" "$specialmode" [ specialmode = $arg1 ]
menuitemcheckbox "JShow 1weapon" "$showweapon" [showweapon = $arg1]
menuitem "browse gema servers" [showmenu "gema servers"]
menuitem "Close"	[ closemenu ]

newmenu "records"
menuitemvar [concat "Jmap : P " (curmap)] [][]
menuitemvar [concat "1Assault Rifle: " (getrecord 6)] [(delalias (concatword (curmap) "6"))] []
menuitemvar [concat "1Submachine Gun: " (getrecord 4)] [(delalias (concatword (curmap) "4"))] []
menuitemvar [concat "1Sniper Rifle: " (getrecord 5)] [(delalias (concatword (curmap) "5"))] []
menuitemvar [concat "1Shotgun: " (getrecord 3)] [(delalias (concatword (curmap) "3"))] []
menuitemvar [concat "1Carbine: " (getrecord 2)] [(delalias (concatword (curmap) "2"))] []
menuitemvar [concat "1Pistol only: " (getrecord 1)] [(delalias (concatword (curmap) "1"))] []
menuitemvar [concat "1Knife only: " (getrecord 0)] [(delalias (concatword (curmap) "0"))] []

bind l [showmenu "gema offline"]

newmenu "gema servers"
menuitem "GEMA Avengers" [connect 84.200.52.36 28763]
menuitem "Gema Central" [connect 185.38.45.43 1111]
menuitem "Silvercloud GEMAS" [connect 162.220.8.73 28700]

bind return respawn
